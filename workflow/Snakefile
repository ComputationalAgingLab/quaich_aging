import os
from os import path
from glob import glob
import cooler
import numpy as np
import pandas as pd

localrules: all, make_differential_insulation, make_tads

genome = config['genome']
path_genomes = config['path_genomes'].rstrip('/')

project_folder = config['project_folder']

coolers_folder = config.get('coolers_folder', path.join(project_folder, 'coolers'))
beds_folder = path.normpath(config.get('beds_folder', path.join(project_folder, 'beds')))
beds_folder_name = path.basename(beds_folder)
expected_folder = path.normpath(config.get('expected_folder', path.join(project_folder, 'expected')))
pileups_folder = path.normpath(config.get('pileups_folder', path.join(project_folder, 'pileups')))
eigenvectors_folder = path.normpath(config.get('eigenvectors_folder', path.join(project_folder, 'eigenvectors')))
saddles_folder = path.normpath(config.get('saddles_folder', path.join(project_folder, 'saddles')))
insulation_folder = path.normpath(config.get('insulation_folder', path.join(project_folder, 'insulation')))
tad_folder = path.normpath(config.get('TAD_folder', path.join(project_folder, 'TADs')))
tad_folder_name = path.basename(tad_folder)
loop_folder = path.normpath(config.get('loop_folder', path.join(project_folder, 'Loops')))
loop_folder_name = path.basename(loop_folder)
boundary_folder = path.normpath(config.get('boundary_folder', path.join(project_folder, 'Boundaries')))
boundary_folder_name = path.basename(boundary_folder)
outfolders = {tad_folder_name:tad_folder,
              loop_folder_name:loop_folder,
              boundary_folder_name:boundary_folder,
              beds_folder_name:beds_folder}

ignore_resolutions_more_than = config['ignore_resolutions_more_than']
eigenvector_resolution_limits = config['eigenvector_resolution_limits']
pileup_resolution_limits = config['pileup_resolution_limits']
insulation_resolution_limits = config['insulation_resolution_limits']

pileups_mindist, pileups_maxdist = config['pileups_distance_limits']
saddle_mindist, saddle_maxdist = config['saddle_distance_limits']
# bedpe_pileups_mindist, bedpe_pileups_maxdist = config['bedpe_pileups_distance_limits']

shifts = config['shifts']

bedpairs_folder = path.normpath(config['bed_pairs']['path'])
bedpairs_folder_name = path.basename(bedpairs_folder)

def get_pairs(bedpairs_input_dict):
    return bedpairs_input_dict['files']

bedpairs = {name:config['bed_pairs']['input'][name]['files'] for name in config['bed_pairs']['input'].keys()}
bedpairs_args = {name:' '+config['bed_pairs']['input'][name]['arguments'] for name in config['bed_pairs']['input'].keys()}

def get_files(folder, extension):
    files = list(map(path.basename, glob(f'{folder}/*{extension}')))
    return files

coolfiles = get_files(coolers_folder, 'mcool')
samples = [filename.split('.'+genome)[0] for filename in coolfiles]
bedfiles = get_files(beds_folder, 'bed')
##### Assume same resolutions in all coolers
resolutions = config.get('resolutions',
                         list(map(lambda x: int(x.split('/')[-1]),
                           cooler.fileops.list_coolers(path.join(coolers_folder,
                                                       coolfiles[0]))
                                  )))
minresolution = min(resolutions)
resolutions = list(filter(lambda x: x<=ignore_resolutions_more_than, resolutions))
eigenvector_resolutions = list(filter(lambda x: eigenvector_resolution_limits[0]<=x<=eigenvector_resolution_limits[1],
                                      resolutions))
pileup_resolutions = list(filter(lambda x: pileup_resolution_limits[0]<=x<=pileup_resolution_limits[1],
                                 resolutions))
insulation_resolutions = list(filter(lambda x: insulation_resolution_limits[0]<=x<=insulation_resolution_limits[1],
                                 resolutions))
mindists = [int(pileups_mindist*2**i) for i in np.arange(0, np.log2(pileups_maxdist/pileups_mindist))]
separations = [f'_dist_{mindist}-{mindist*2}' for mindist in mindists]


saddle_mindists = [int(saddle_mindist*2**i) for i in np.arange(0, np.log2(saddle_maxdist/saddle_mindist))]
saddle_separations = [f'_dist_{mindist}-{mindist*2}' for mindist in saddle_mindists]
# bedpe_mindists = [int(bedpe_pileups_mindist*2**i) for i in np.arange(0, np.log2(bedpe_pileups_maxdist/bedpe_pileups_mindist))]
# bedpe_separations = [f'{mindist}-{mindist*2}' for mindist in bedpe_mindists]

diff_boundaries = expand(f"{pileups_folder}/{boundary_folder_name}/{{sample}}-{{resolution}}_over_Insulation_{config['compare_boundaries_samples'][0]}_not_"
                         f"{config['compare_boundaries_samples'][1]}_{{insul_resolution}}_{{window}}.bed_{{norm}}_local.np.txt",
                        sample = samples,
                        resolution = pileup_resolutions,
                        insul_resolution=insulation_resolutions,
                        window=config['insulation_windows'],
                        norm=['expected', f'{shifts}-shifts'])
tads = expand(f"{pileups_folder}/{tad_folder_name}/{{sample}}-{{resolution}}_over_TADs_{{sampleTADs}}_{{insul_resolution}}_{{window}}.bed_{{norm}}_local_rescaled.np.txt",
                sample = samples,
                resolution = pileup_resolutions,
                sampleTADs=config['call_TADs_samples'],
                insul_resolution=insulation_resolutions,
                window=config['insulation_windows'],
                norm=['expected', f'{shifts}-shifts'])
loops = expand(f"{pileups_folder}/{loop_folder_name}/{{sample}}-{{resolution}}_over_Loops_{{sampleLoops}}_{{resolutionLoops}}.bedpe_{{norm}}{{mode}}.np.txt",
                sample = samples,
                resolution = pileup_resolutions,
                sampleLoops=config['call_loops_samples'],
                resolutionLoops=config['loop_call_resolution'],
                norm=['expected', f'{shifts}-shifts'],
                mode=['']+separations)
beds = expand(f"{pileups_folder}/{beds_folder_name}/{{sample}}-{{resolution}}_over_{{bedfile}}_{{norm}}{{mode}}.np.txt",
                sample = samples,
                resolution = pileup_resolutions,
                bedfile=bedfiles,
                norm=['expected', f'{shifts}-shifts'],
                mode=['', '_local']+separations)
paired_beds = expand(f'{pileups_folder}/{bedpairs_folder_name}/{{sample}}-{{resolution}}_over-paired_{{pairname}}_{{norm}}{{mode}}.np.txt',
                        sample = samples,
                        resolution = pileup_resolutions,
                        pairname = bedpairs.keys(),
                        separation = separations,
                        norm=['expected', f'{shifts}-shifts'],
                        mode=['']+separations)
saddles = expand(f'{saddles_folder}/{{sample}}_{{resolution}}_{{bins}}{{dist}}.{{ending}}',
                        sample = samples,
                        resolution = eigenvector_resolutions,
                        bins = config['saddle_bins'],
                        dist = saddle_separations+[""],
                        ending = ['saddledump.npz', 'digitized.tsv'])

def split_dist(dist_wildcard, mindist_arg='--mindist', maxdist_arg='--maxdist'):
    if dist_wildcard=="":
        return ""
    else:
        assert dist_wildcard.startswith('_dist_')
        dists = dist_wildcard.split('_')[-1]
        mindist, maxdist = dists.split('-')
        return f'{mindist_arg} {mindist} {maxdist_arg} {maxdist}'

rule all:
    input:
        lambda wildcards: expand(f'{expected_folder}/{{sample}}_{{resolution}}.expected.tsv',
                                sample = samples,
                                resolution = resolutions),
        lambda wildcards: expand(f'{eigenvectors_folder}/{{sample}}_{{resolution}}_eigenvectors.cis.{{ending}}',
                                sample = samples,
                                resolution = eigenvector_resolutions,
                                ending = ['vecs.tsv', 'lam.txt', 'bw']),
        lambda wildcards: expand(f'{insulation_folder}/{{sample}}_{{resolution}}_{{window}}.insulation.tsv',
                                sample = samples,
                                resolution = insulation_resolutions,
                                window = config['insulation_windows']),
        lambda wildcards: diff_boundaries,
        lambda wildcards: tads,
        lambda wildcards: loops,
        lambda wildcards: beds,
        lambda wildcards: paired_beds,
        lambda wildcards: saddles,

rule make_pileups:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        expected = f'{expected_folder}/{{sample}}_{{resolution}}.expected.tsv',
        bedfile = lambda wildcards: f'{outfolders[wildcards.folder]}/{wildcards.bedfile}'
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over_{{bedfile}}_expected.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{wildcards.folder}',
    threads: 8
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bedfile}} --n_proc {{threads}} --expected {{input.expected}} --outdir {{params.outdir}} --outname {{params.outname}}"

rule make_pileups_local:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        expected = f'{expected_folder}/{{sample}}_{{resolution}}.expected.tsv',
        bedfile = lambda wildcards: f'{outfolders[wildcards.folder]}/{wildcards.bedfile}'
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over_{{bedfile}}_expected_local.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{wildcards.folder}',
    threads: 4
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bedfile}} --local --n_proc {{threads}} --expected {{input.expected}} --outdir {{params.outdir}} --outname {{params.outname}}"

rule make_pileups_local_rescaled:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        expected = f'{expected_folder}/{{sample}}_{{resolution}}.expected.tsv',
        bedfile = lambda wildcards: f'{outfolders[wildcards.folder]}/{wildcards.bedfile}'
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over_{{bedfile}}_expected_local_rescaled.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{wildcards.folder}',
    threads: 4
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bedfile}} --local --rescale --rescale_pad {config['rescale_pad']} --n_proc {{threads}} --expected {{input.expected}} --outdir {{params.outdir}} --outname {{params.outname}}"

rule make_pileups_distance:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        expected = f'{expected_folder}/{{sample}}_{{resolution}}.expected.tsv',
        bedfile = lambda wildcards: f'{outfolders[wildcards.folder]}/{wildcards.bedfile}'
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over_{{bedfile}}_expected_dist_{{mindist,[0-9]+}}-{{maxdist,[0-9]+}}.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{wildcards.folder}',
    threads: 4
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bedfile}} --n_proc {{threads}} --expected {{input.expected}} --mindist {{wildcards.mindist}} --maxdist {{wildcards.maxdist}} --outdir {{params.outdir}} --outname {{params.outname}}"

rule make_pileups_shifts:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        bedfile = lambda wildcards: f'{outfolders[wildcards.folder]}/{wildcards.bedfile}'
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over_{{bedfile}}_{shifts}-shifts.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{wildcards.folder}',
    threads: 8
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bedfile}} --n_proc {{threads}} --nshifts {shifts} --outdir {{params.outdir}} --outname {{params.outname}}"

rule make_pileups_shifts_local:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        bedfile = lambda wildcards: f'{outfolders[wildcards.folder]}/{wildcards.bedfile}'
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over_{{bedfile}}_{shifts}-shifts_local.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{wildcards.folder}',
    threads: 4
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bedfile}} --local --n_proc {{threads}} --nshifts {shifts} --outdir {{params.outdir}} --outname {{params.outname}}"

rule make_pileups_shifts_local_rescaled:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        bedfile = lambda wildcards: f'{outfolders[wildcards.folder]}/{wildcards.bedfile}'
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over_{{bedfile}}_{shifts}-shifts_local_rescaled.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{wildcards.folder}',
    threads: 4
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bedfile}} --local --rescale --rescale_pad {config['rescale_pad']} --n_proc {{threads}} --nshifts {shifts} --outdir {{params.outdir}} --outname {{params.outname}}"

rule make_pileups_shifts_distance:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        bedfile = lambda wildcards: f'{outfolders[wildcards.folder]}/{wildcards.bedfile}'
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over_{{bedfile}}_{shifts}-shifts_dist_{{mindist,[0-9]+}}-{{maxdist,[0-9]+}}.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{wildcards.folder}',
    threads: 4
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bedfile}} --n_proc {{threads}} --nshifts {shifts} --mindist {{wildcards.mindist}} --maxdist {{wildcards.maxdist}} --outdir {{params.outdir}} --outname {{params.outname}}"

rule make_pileups_bed2:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        expected = f'{expected_folder}/{{sample}}_{{resolution}}.expected.tsv',
        bed1 = lambda wildcards: path.join(bedpairs_folder, bedpairs[wildcards.pairname][0]),
        bed2 = lambda wildcards: path.join(bedpairs_folder, bedpairs[wildcards.pairname][1]),
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over-paired_{{pairname}}_expected.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{bedpairs_folder_name}',
        args = lambda wildcards: bedpairs_args[wildcards.pairname]
    threads: 8
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bed1}} --bed2 {{input.bed2}} --n_proc {{threads}} --expected {{input.expected}} --outdir {{params.outdir}} --outname {{params.outname}} {{params.args}}"

rule make_pileups_bed2_distance:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        expected = f'{expected_folder}/{{sample}}_{{resolution}}.expected.tsv',
        bed1 = lambda wildcards: path.join(bedpairs_folder, bedpairs[wildcards.pairname][0]),
        bed2 = lambda wildcards: path.join(bedpairs_folder, bedpairs[wildcards.pairname][1]),
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over-paired_{{pairname}}_expected_dist_{{mindist,[0-9]+}}-{{maxdist,[0-9]+}}.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{wildcards.folder}',
        args = lambda wildcards: bedpairs_args[wildcards.pairname]
    threads: 4
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bed1}} --bed2 {{input.bed2}} --n_proc {{threads}} --expected {{input.expected}} --mindist {{wildcards.mindist}} --maxdist {{wildcards.maxdist}} --outdir {{params.outdir}} --outname {{params.outname}} {{params.args}}"

rule make_pileups_bed2_shifts:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        bed1 = lambda wildcards: path.join(bedpairs_folder, bedpairs[wildcards.pairname][0]),
        bed2 = lambda wildcards: path.join(bedpairs_folder, bedpairs[wildcards.pairname][1]),
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over-paired_{{pairname}}_{shifts}-shifts.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{wildcards.folder}',
        args = lambda wildcards: bedpairs_args[wildcards.pairname]
    threads: 8
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bed1}} --bed2 {{input.bed2}} --n_proc {{threads}} --nshifts {shifts} --outdir {{params.outdir}} --outname {{params.outname}} {{params.args}}"

rule make_pileups_bed2_shifts_distance:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
        bed1 = lambda wildcards: path.join(bedpairs_folder, bedpairs[wildcards.pairname][0]),
        bed2 = lambda wildcards: path.join(bedpairs_folder, bedpairs[wildcards.pairname][1]),
    output:
        f'{pileups_folder}/{{folder}}/{{sample}}-{{resolution,[0-9]+}}_over-paired_{{pairname}}_{shifts}-shifts_dist_{{mindist,[0-9]+}}-{{maxdist,[0-9]+}}.np.txt'
    params:
        outname = lambda wildcards, output: output[0].split('/')[-1],
        outdir = lambda wildcards, output: f'{pileups_folder}/{wildcards.folder}',
        args = lambda wildcards: bedpairs_args[wildcards.pairname]
    threads: 4
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"coolpup.py {{input.cooler}}::resolutions/{{wildcards.resolution}} {{input.bed1}} --bed2 {{input.bed2}} --n_proc {{threads}} --nshifts {shifts} --mindist {{wildcards.mindist}} --maxdist {{wildcards.maxdist}} --outdir {{params.outdir}} --outname {{params.outname}} {{params.args}}"

rule call_loops:
    input:
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
    output:
        f'{loop_folder}/loops_{{sample}}_{{resolution,[0-9]+}}.bedpe',
    threads: 4
    resources:
        mem_mb = lambda wildcards, threads: threads*16*1024,
        runtime = 24*60
    shell:
        f"chromosight detect --pattern loops --no-plotting -t {{threads}} {{input.cooler}}::resolutions/{{wildcards.resolution}} {loop_folder}/loops_{{wildcards.sample}}_{{wildcards.resolution}} &&"
        f"tail -n +2 {loop_folder}/loops_{{wildcards.sample}}_{{wildcards.resolution}}/loops_out.txt | cut -f1-6 > {{output}}"

rule make_differential_insulation:
    input:
        insulation_WT = f'{insulation_folder}/{{sampleWT}}_{{resolution}}_{{window}}.insulation.tsv',
        insulation_KO = f'{insulation_folder}/{{sampleKO}}_{{resolution}}_{{window}}.insulation.tsv'
    output:
        f'{boundary_folder}/Insulation_{{sampleWT}}_not_{{sampleKO}}_{{resolution,[0-9]+}}_{{window,[0-9]+}}.bed'
    threads: 1
    resources:
        mem_mb = 1024,
        runtime = 60
    run:
        insWT = pd.read_csv(input.insulation_WT, sep='\t')
        insWT = insWT[~insWT['is_bad_bin']].drop(columns=['is_bad_bin'])
        insKO = pd.read_csv(input.insulation_KO, sep='\t')
        insKO = insKO[~insKO['is_bad_bin']].drop(columns=['is_bad_bin'])
        ins = pd.merge(insWT, insKO, suffixes=('WT', 'KO'), on=['chrom', 'start', 'end'])
        diff_ins = ins[(ins[f'boundary_strength_{wildcards.window}WT']/ins[f'boundary_strength_{wildcards.window}KO']>=config['insulation_fold_change_threshold']) |
        ((ins[f'boundary_strength_{wildcards.window}WT']>config['insulation_strength_threshold']) & np.isnan(ins[f'boundary_strength_{wildcards.window}KO']))]
        diff_ins[['chrom', 'start', 'end']].to_csv(output[0], header=False, index=False, sep='\t')

rule make_tads:
    input:
        insulation = f'{insulation_folder}/{{sample}}_{{resolution}}_{{window}}.insulation.tsv'
    output:
        f'{tad_folder}/TADs_{{sample}}_{{resolution,[0-9]+}}_{{window,[0-9]+}}.bed'
    threads: 1
    resources:
        mem_mb = 1024,
        runtime = 60
    run:
        ins = pd.read_csv(input.insulation, sep='\t')
        ins = ins[~ins['is_bad_bin']].drop(columns=['is_bad_bin'])
        ins = ins[ins[f'boundary_strength_{wildcards.window}']>config['insulation_strength_threshold']][['chrom', 'start', 'end']]
        tads = ins.groupby('chrom').apply(lambda x: pd.concat([x[:-1].reset_index(drop=True), x[1:].reset_index(drop=True)], axis=1, ignore_index=True)).reset_index(drop=True)
        tads.columns=[['chrom1', 'start1', 'end1', 'chrom2', 'start2', 'end2']]
        tads.columns = tads.columns.get_level_values(0)
        tads = tads[(tads['start2']-tads['start1'])<=config['max_tad_length']].reset_index(drop=True)
        tads['start'] = (tads['start1']+tads['end1'])//2
        tads['end'] = (tads['start2']+tads['end2'])//2
        tads = tads[['chrom1', 'start', 'end']]
        tads.to_csv(output[0], header=False, index=False, sep='\t')

rule make_insulation:
    input:
        f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
    output:
        f'{insulation_folder}/{{sample}}_{{resolution,[0-9]+}}_{{window,[0-9]+}}.insulation.tsv'
    params:
        chunksize = config['insulation_chunksize']
    threads: 1
    resources:
        mem_mb = 32*1024,
        runtime = 60
    shell:
        "cooltools diamond-insulation {input}::resolutions/{wildcards.resolution} {wildcards.window} --chunksize {params.chunksize} > {output}"

rule make_saddles:
    input:
        eigenvector = f'{eigenvectors_folder}/{{sample}}_{{resolution}}_eigenvectors.cis.vecs.tsv',
        expected = f'{expected_folder}/{{sample}}_{{resolution}}.expected.tsv',
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
    output:
        saddle = f'{saddles_folder}/{{sample}}_{{resolution,[0-9]+}}_{{bins,[0-9]+}}{{dist,.*}}.saddledump.npz',
        digitized = f'{saddles_folder}/{{sample}}_{{resolution,[0-9]+}}_{{bins,[0-9]+}}{{dist,.*}}.digitized.tsv',
    params:
        prefix = lambda wildcards, output: output['saddle'][:-15],
        distargs = lambda wildcards: split_dist(wildcards.dist, '--min-dist', '--max-dist'),
        args = config['saddle_options']
    threads: 1
    resources:
        mem_mb = 8*1024,
        runtime = 60
    shell:
        "cooltools compute-saddle -o {params.prefix} {params.args} {params.distargs} {input.cooler}::resolutions/{wildcards.resolution} {input.eigenvector} {input.expected}"

rule make_expected:
    input:
        f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
    output:
        f'{expected_folder}/{{sample}}_{{resolution,[0-9]+}}.expected.tsv'
    threads: 4
    resources:
        mem_mb = lambda wildcards, threads: threads*8*1024,
        runtime = 60
    shell:
        "cooltools compute-expected -p {threads} {input}::resolutions/{wildcards.resolution} --ignore-diags 0 -o {output}"

rule make_eigenvectors:
    input:
        reftrack = f'{path_genomes}/{genome}/gc/{genome}_{{resolution}}_gc.bedgraph',
        cooler = f'{coolers_folder}/{{sample}}.{genome}.{config["filter"]}.{minresolution}.mcool',
    output:
        f'{eigenvectors_folder}/{{sample}}_{{resolution,[0-9]+}}_eigenvectors.cis.vecs.tsv',
        f'{eigenvectors_folder}/{{sample}}_{{resolution,[0-9]+}}_eigenvectors.cis.lam.txt',
        f'{eigenvectors_folder}/{{sample}}_{{resolution,[0-9]+}}_eigenvectors.cis.bw'
    params:
        prefix = lambda wildcards, output: output[0][:-13]
    threads: 1
    resources:
        mem_mb = 8*1024,
        runtime = 60
    shell:
        "cooltools call-compartments --reference-track {input.reftrack} --bigwig {input.cooler}::resolutions/{wildcards.resolution} -o {params.prefix}"

rule make_gc:
    input:
        fasta = f'{path_genomes}/{genome}/{genome}.fa',
        bins = f'{path_genomes}/{genome}/bins/{genome}_{{resolution}}_bins.bed'
    output:
        f'{path_genomes}/{genome}/gc/{genome}_{{resolution,[0-9]+}}_gc.bedgraph'
    threads: 1
    resources:
        mem_mb = 8*1024,
        runtime = 60
    shell:
        "cooltools genome gc {input.bins} {input.fasta} > {output}"

rule make_bins:
    input:
        chromsizes = f'{path_genomes}/{genome}/chrfile.txt'
    output:
        f'{path_genomes}/{genome}/bins/{genome}_{{resolution,[0-9]+}}_bins.bed'
    threads: 1
    resources:
        mem_mb = 8*1024,
        runtime = 60
    shell:
        "cooltools genome binnify {input} {wildcards.resolution} > {output}"
